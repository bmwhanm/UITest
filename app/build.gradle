apply plugin: 'com.android.application'
apply plugin: 'com.antfortune.freeline'


def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd-HH-mm')
    return formattedDate
}

def verCode = 2


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.hq.uitest"
        minSdkVersion 16
        targetSdkVersion 26
        multiDexEnabled true
        versionCode verCode
        versionName APP_VERSION
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }


    signingConfigs {
        signingConfig {
            //由于本地打包使用的是本机上的KeyStore
            //而Jenkins打包用的是服务器上的KeyStore
            //两个路径不一样
            if("true".equals(IS_JENKINS)){
                storeFile file("服务器上KeyStore的路径")
            }else {
                storeFile file(STORE_FILE_PATH)
            }
        }
    }


    buildTypes {

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signingConfig
        }
        debug {  }
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    //渠道Flavors
    productFlavors {
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
        xiaomi {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
    }




    lintOptions {
        abortOnError true
    }


    //修改生成的apk名字及输出文件夹
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            //新名字
            def newName
            //时间戳
            def timeNow
            //输出文件夹
            def outDirectory
            //是否为Jenkins打包，输出路径不同
            if ("true".equals(IS_JENKINS)) {
                //Jenkins打包输出到服务器路径
                timeNow = BUILD_TIME
                //BUILD_PATH为服务器输出路径
                outDirectory = './'
                //AutoBuildTest-v1.0.1-xiaomi-release.apk
                newName = 'AutoBuildTest-v' + APP_VERSION + '-' + variant.productFlavors[0].name + '-' + variant.buildType.name + '.apk'
            } else {
                //本机打包输出在本机路径
                timeNow = getDate()
                outDirectory = output.outputFile.getParent()
                if ('debug'.equals(variant.buildType.name)) {
                    newName = "AutoBuildTest-v${APP_VERSION}-debug.apk"
                } else {
                    //AutoBuildTest-v1.0.1-xiaomi-release.apk
                    newName = 'AutoBuildTest-v' + APP_VERSION + '-' + variant.productFlavors[0].name + '-' + variant.buildType.name + '.apk'
                }
            }
            output.outputFile = new File(outDirectory+'/'+timeNow, newName)
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.alibaba.android:vlayout:1.2.2@aar') {
        transitive = true
    }
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.alibaba:arouter-api:1.2.2'
    compile 'com.alibaba.android:tangram:1.0.0@aar'
    compile 'com.facebook.fresco:fresco:1.4.0'
    compile 'com.facebook.fresco:animated-gif:1.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.bumptech.glide:glide:3.5.2'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.3'
    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'com.github.dmytrodanylyk.shadow-layout:library:1.0.3'
}
